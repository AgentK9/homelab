import "./deploy_k8s.pkl"

import "./ingressroute.pkl"
import "./certificate.pkl"
import "./immich.pkl"
import "./secrets.pkl"
import "./metallb.pkl"

class InputChart {
    name: String
    namespace: String = name
    service_name: String = name
    port: Int
}


charts: Listing<InputChart>

local base_objects: Listing = new {
    for (chart in charts) {
        new ingressroute.IngressRoute {
            dnsPrefix = chart.name
            svc_port = chart.port
            svc_name = chart.service_name
            metadata {
                namespace = chart.namespace
            }
        }
        new certificate.Certificate {
            dnsPrefix = chart.name
            metadata {
                namespace = chart.namespace
            }
        }
    }
    immich.pvc
    secrets.cloudflare_secret
    secrets.clusterissuer
    metallb.pool
    metallb.advertisement
}

objects = (base_objects) {
    [[metadata.namespace == "homer" && (this is certificate.Certificate || this is ingressroute.IngressRoute)]] {
        dnsPrefix = null
    }
}

output = deploy_k8s.getK8sOutputs(objects)
